behavior ImageMChoiceTextExercise
    def selectChoice(choice)
        if @exit of #exercise-options
            exit
        end
        for choiceElement in document.querySelectorAll('#exercise-options button')
            remove @selected from choiceElement
            remove .text-indigo-600 from choiceElement
            add .text-gray-600 .hover:text-indigo-600 to choiceElement
        end
        add @selected to choice
        add .text-indigo-600 to choice
        remove .text-gray-600 .hover:text-indigo-600 from choice
    end

    def setChoiceInfo(correctChoice)
        set #exercise-options@exit to true
        for choiceElement in document.querySelectorAll('#exercise-options button')
            remove @selected from choiceElement
            remove .text-indigo-600 .hover:text-indigo-600 .text-gray-600 from choiceElement
            if @value of choiceElement == correctChoice 
                add .text-green-500 to choiceElement
            else
                add .text-red-500 to choiceElement
            end
        end 
    end

    def getAnswer()
        set button to the first <button[selected]/>
        if button == null
            exit
        end
        return {"term_id": @value of button} 
    end

    def checkAnswer(url)
        call checkExercise(url, getAnswer()) then put it into exerciseResult
        call setMessageContent(exerciseResult)
        call setChoiceInfo(exerciseResult.correct_answer)
        wait 4s then call loadExercise()
    end
end